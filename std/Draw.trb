package std

func draw(
    id: Number,
    x1: Number, y1: Number,
    x2: Number, y2: Number,
    x3: Number, y3: Number,
    x4: Number, y4: Number,
    z: Number, alpha: Number,
) {
    builtins.Draw.statement(
        id,
        x1, y1,
        x2, y2,
        x3, y3,
        x4, y4,
        z, alpha,
    )
}

struct Drawable(quad: Quad, id: Number, z: Number, alpha: Number = 1)
func Drawable.draw() = this.quad.draw(this.id, this.z, this.alpha)

// p1 is bottom left; points go clockwise.
struct Quad(p1: Point, p2: Point, p3: Point, p4: Point)

func Quad.draw(id: Number, z: Number, alpha: Number = 1) {
    draw(
        id,
        this.p1.x, this.p1.y,
        this.p2.x, this.p2.y,
        this.p3.x, this.p3.y,
        this.p4.x, this.p4.y,
        z, alpha,
    )
}

func Quad.spawnParticle(id: Number, t: Number, loop: Boolean = false): ParticleID {
    spawnParticle(
        id,
        this.p1.x, this.p1.y,
        this.p2.x, this.p2.y,
        this.p3.x, this.p3.y,
        this.p4.x, this.p4.y,
        t, loop,
    )
}

func Quad.rotatedCW() = Quad(this.p4, this.p1, this.p2, this.p3)
func Quad.rotatedCCW() = Quad(this.p2, this.p3, this.p4, this.p1)
func Quad.rotated180() = Quad(this.p3, this.p4, this.p1, this.p2)

func centerRectangle(center: Point, halfLength: Number, halfWidth: Number) = Quad(
    center.translated(-halfLength, -halfWidth),
    center.translated(-halfLength, halfWidth),
    center.translated(halfLength, halfWidth),
    center.translated(halfLength, -halfWidth),
)

// Rectangle with bottom left point specified
func cornerRectangle(corner: Point, length: Number, width: Number) = Quad(
    corner,
    corner.translated(0, width),
    corner.translated(length, width),
    corner.translated(length, 0),
)

func centerSquare(center: Point, halfWidth: Number) = centerRectangle(center, halfWidth, halfWidth)
func cornerSquare(corner: Point, halfWidth: Number) = cornerRectangle(corner, halfWidth, halfWidth)

func offsetRectangle(top: Number, bottom: Number, left: Number, right: Number) = Quad(
    Point(left, bottom),
    Point(left, top),
    Point(right, top),
    Point(right, bottom),
)

let drawCurvedL = builtins.DrawCurvedL.statement
let drawCurvedR = builtins.DrawCurvedR.statement
let drawCurvedLR = builtins.DrawCurvedLR.statement
let drawCurvedB = builtins.DrawCurvedB.statement
let drawCurvedT = builtins.DrawCurvedT.statement
let drawCurvedBT = builtins.DrawCurvedBT.statement
